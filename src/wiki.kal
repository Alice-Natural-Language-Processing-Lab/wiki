###
Requires
###
request = require "request"

###
Defaults
###
DEFAULT_API_URL = "http://en.wikipedia.org/w/api.php"
DEFAULT_LIMIT = 10
DEFAULT_SUGGESTION = no
DEFAULT_AUTO_SUGGEST = yes
DEFAULT_REDIRECT = yes
DEFAULT_PRELOAD = no
DEFAULT_ORIGINAL_TITLE = ""
DEFAULT_PAGES = 1
DEFAULT_MAX = "max"
DEFAULT_RADIUS = 1000 # 1km
DEFAULT_VERBOSE = yes

###
Base Wikipedia Interface
###
Wiki =
	apiUrl: DEFAULT_API_URL
	verbose: DEFAULT_VERBOSE

###
Basic request method
@param {object} params - Parameters for the HTTP request
###
Wiki.request = task(params)
	params.format = "json"
	params.action = "query"
	options =
		uri: Wiki.apiUrl
		qs: params
		headers:
			"User-Agent": "WikiJs/0.1 (https://github.com/rompetoto/wiki; richard.vanderdys@gmail.com)"
	wait for e, res, body from request.get options
	return JSON.parse res

###
Searches Wikipedia
@param {string} query - The search query.
@param {number} limit - The number of results. (Optional: Default is 10)
@param {boolean} suggestion - Allow  Wikipedia to return a suggested article (Optional: Default is true)
@param {function} callback - Callback with parameters (error, results, suggestion)
###
Wiki.search = task(query, limit = DEFAULT_LIMIT, suggestion = DEFAULT_SUGGESTION)
	params =
		list: "search"
		srprop: ""
		srlimit: limit
		srsearch: query

	params.srinfo = "suggestion" when suggestion is yes
	wait for res from Wiki.request params
	results = [item.title for item in res.query.search]

	when suggestion is yes and res.query.searchinfo exists
		return results, res.query.searchinfo.suggestion
	
	return results

###
Obtain random articles from Wikipedia
@param {number} pages - The number of random articles. (Optional: Default is 1)
@param {function} callback - Callback with parameters (error, results)
###
Wiki.random = task(pages = DEFAULT_PAGES)
	wait for res from Wiki.request(
		list: "random"
		rnnamespace: 0
		rnlimit: pages
	)
	return [page.title for page in res.query.random]

###
Obtain a WikiPage object by article title
@param {string} title - Article title
@param {boolean} autoSuggest - Allow Wikipedia to return a suggested article (Optional: Default is true)
@param {function} callback - Callback with parameters (error, page)
###
Wiki.page = task(title, autoSuggest = DEFAULT_AUTO_SUGGEST)	
	if autoSuggest is yes
		wait for results, suggestion from Wiki.search title, 1, yes
		try
			title = suggestion or results[0]
			page = new WikiPage title
			wait for code from page.load()
			return page
		catch e
			throw
				name: "PageError"
				message: title
				original: e
	else	
		page = new WikiPage title
		wait for code from page.load()
		return page

###
Perform a search by geographical coordinate
@param {number} lat - Latitude
@param {number} lon - Longitude
@param {number} radius - Radial search distance in meters (Optional: Default is 1km)
@param {function} callback - Callback with parameters (error, results)
###
Wiki.geoSearch = task(lat, lon, radius = DEFAULT_RADIUS)
	wait for res from Wiki.request(
		list: "geosearch"
		gsradius: radius
		gscoord: lat + '|' + lon
	)
	return [ page.title for page in res.query.geosearch ]

class WikiPage
	
	method initialize(title, originalTitle)
		
		me.title = title
		me.originalTitle = originalTitle or title
	
	task load()
		wait for res from Wiki.request(
			prop: "info|pageprops"
			inprop: "url"
			ppprop: "disambiguation"
			titles: me.title
		)

		me.pageId = Object.keys(res.query.pages)[0]
		me.url = res.query.pages[me.pageId].fullurl

		return yes

	###
	Helper Functions
	###

	task _helper(name, params)
		when me[name] exists
			return me[name]
		otherwise
			wait for res from Wiki.request params
			me[name] = res.query.pages[me.pageId].extract
			return me[name]

	task queryContinue(params, key, limit, handler, counter = 0, start = no)
		params[key + "continue"] = start when start isnt no
		params[key + "limit"] = limit
		wait for res from Wiki.request params
		when params.list exists
			data = res.query[params.list]
		otherwise
			data = res.query.pages[me.pageId][params.prop]
		counter += data.length
		handler data
		params[key + "continue"] = null
		if res["query-continue"] exists and (limit is DEFAULT_MAX or counter < limit)
			when params.list exists
				startFrom = res["query-continue"][params.list][key + "continue"]
			otherwise
				startFrom = res["query-continue"][params.prop][key + "continue"]
			wait for done from me.queryContinue params, key, limit, handler, counter, startFrom
			return done
	
	###
	Get the HTML from an article
	@param {function} callback - Callback with parameters (error, html)
	###
	task html()
		when me._html exists
			return me._html
		otherwise
			params = 
				prop: "revisions"
				rvprop: "content"
				rvlimit: 1
				rvparse: ""
				titles: me.title
			wait for res from Wiki.request params
			me._html = res.query.pages[me.pageId].revisions[0]["*"]
			return me._html

	###
	Get the text content from an article
	@param {function} callback - Callback with parameters (error, content)
	###
	task content()
		params = 
			prop: "extracts"
			explaintext: ""
			titles: me.title
		wait for data from me._helper "_content", params
		return data

	###
	Get the text summary from an article
	@param {function} callback - Callback with parameters (error, summary)
	###
	task summary()
		params =
			prop: "extracts"
			explaintext: ""
			exintro: ""
			titles: me.title
		wait for data from me._helper "_summary", params
		return data

	###
	Get list of image URLs from an article
	@param {function} callback - Callback with parameters (error, images)
	###
	task images()
		when me._images exists
			return me._images
		otherwise
			wait for res from Wiki.request(
				generator: "images"
				gimlimit: "max"
				prop: "imageinfo"
				iiprop: "url"
				titles: me.title
			)
			me._images = [image.imageinfo[0].url for property value image in res.query.pages]
			return me._images

	###
	Get list of reference URLs from an article
	@param {function} callback - Callback with parameters (error, refs)
	###
	task references()
		when me._references exists
			return me._references
		otherwise
			wait for res from Wiki.request(
				prop: "extlinks"
				ellimit: "max"
				titles: me.title
			)
			links = res.query.pages[me.pageId].extlinks
			me._references = [addProtocol(link["*"]) for link in links]
			return me._references

	###
	Get list of links from an article
	@param {number} limit - Number of results
	@param {function} callback - Callback with parameters (error, links)
	###
	task links(limit = DEFAULT_MAX)
		when me._links exists
			return me._links
		otherwise
			me._links = []
			params =
				prop: "links"
				plnamespace: 0
				titles: me.title
			wait for done from me.queryContinue(params, "pl", limit, (data) ->
				me._links.extend [ link.title for link in data ]
			)
			return me._links

	###
	Check if page is within a given category...
	@param {string} category - Category to check
	@param {function} callback - Callback with parameters (error, result)
	###
	task withinCategory(category)
		when me._categories exists
			return me._categories.contains category
		otherwise
			wait for res from Wiki.request(
				prop: "categories"
				cllimit: 1
				clcategories: category
				titles: me.title
			)
			return res.query.pages[me.pageId].categories.length is 1

	###
	Get titles of categories the page belongs to
	@param {number} limit - Number of results
	@param {function} callback - Callback with parameters (error, categories)
	###
	task categories(limit = DEFAULT_MAX)
		when me._categories exists
			return me._categories
		otherwise
			me._categories = []
			params =
				prop: "categories"
				titles: me.title
			wait for done from me.queryContinue(params, "cl", limit, (data) ->
				me._categories.extend [ category.title for category in data ]
			)
			return me._categories

	###
	Get the geographical coordinates of a (location) article
	@param {function} callback - Callback with parameters (error, coordinates)
	###
	task coordinates()
		when me._coordinates exists
			return me._coordinates
		otherwise
			wait for res from Wiki.request(
				prop: "coordinates"
				titles: me.title
			)
			me._coordinates = res.query.pages[me.pageId].coordinates[0]
			return me._coordinates

	###
	Get the infobox of an article, in wikitext
	@param {function} callback - Callback with parameters (error, wikitext)
	###
	task infobox()
		when me._infobox exists
			return me._infobox
		otherwise
			wait for res from Wiki.request(
				prop: "revisions"
				rvprop: "content"
				rvsection: 0
				titles: me.title
			)
			me._infobox = res.query.pages[me.pageId].revisions[0]["*"]
			return me._infobox

	###
	Get the info of an article, in object form
	@param {function} callback - Callback with parameters (error, info)
	###
	task info()
		when me._info exists
			return me._info
		otherwise
			me._info = {}
			wait for text from me.infobox()
			text = text.substring text.indexOf '|'
			text = text.substring 0, text.lastIndexOf '}}'
			data = text.split '\n'
			data.forEach((e) ->
				index = e.indexOf '='
				if index isnt -1
					key = e.substring(1, index).trim().replace(/\s/g, '_')
					val = e.substring(index + 1, e.length).trim().replace(/[^\w()\s.]/g, '')
					if key.length isnt 0 and val.length isnt 0
						me._info[key] = val
			)
			me._info[Object.keys(me._info).pop()] = null
			return me._info

	###
	Get titles of backlinks to the article
	@param {number} limit - Number of results
	@param {function} callback - Callback with parameters (error, backlinks)
	###
	task backlinks(limit = DEFAULT_MAX)
		when me._backlinks exists
			return me._backlinks
		otherwise
			me._backlinks = []
			params =
				list: "backlinks"
				bltitle: me.title
			wait for done from me.queryContinue(params, "bl", limit, (data) ->
				me._backlinks.extend [ link.title for link in data ]
			)
			return me._backlinks
			 
Array.prototype.extend = (array) ->
	me.push.apply me, array

Array.prototype.contains = (i) ->
	return me.indexOf(i) isnt -1

String.prototype.startsWith = (str) ->
	return me.indexOf(str) is 0

addProtocol = (url) ->
	return url if url.startsWith("http") otherwise "http:" + url

log = (x) ->
	console.log x when Wiki.verbose is yes	

module.exports = Wiki